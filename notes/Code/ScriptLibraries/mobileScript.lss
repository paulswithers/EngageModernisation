'++LotusScript Development Environment:2:5:(Options):0:74
%REM
	Library mobileScript
	Created May 1, 2019 by Paul Withers/Intec
	Description: Comments for Library
%END REM
Option Public
Option Declare


'++LotusScript Development Environment:2:5:(Forward):0:1
Declare Sub initialiseDatabase()
Declare Sub exitViewSelectField()
Declare Sub emailExport(stateCity As String)
Declare Sub exitCitiesField
Declare Sub exitStatesField

'++LotusScript Development Environment:2:5:(Declarations):0:2

'++LotusScript Development Environment:2:2:initialiseDatabase:1:8
Sub initialiseDatabase()
	
	Dim session As New NotesSession
	Dim server As String
	
	On Error GoTo showErr
	
	server = session.Currentdatabase.Server
	If (server <> "") Then
		Call session.SetEnvironmentVar("xdemo_server", server)
		' Shhh! undocumented function
		Call session.SetEnvironmentVar("xdemo_hostname", "https://" + session.internethostname + "." + session.internetdomainname)
		Call session.Setenvironmentvar("xdemo_filepath", Replace(session.Currentdatabase.Filepath, "\", "/"))
	End If
Exit Sub
	
showErr:
	MsgBox "Error " & CStr(Err) & " on line " & CStr(Erl) & ": " & Error(Err)
	Exit sub
	
End Sub

'++LotusScript Development Environment:2:2:exitViewSelectField:5:8
%REM
	Sub exitViewSelectField
	Description: Comments for Sub
%END REM
Sub exitViewSelectField()

	Dim ws As New NotesUIWorkspace
	Dim session As New NotesSession
	Dim db As NotesDatabase
	Dim uidoc As NotesUIDocument
	Dim fieldVal As String, viewName As String
	
	On Error GoTo showErr
	
	Set uidoc = ws.CurrentDocument
	fieldVal = uidoc.FieldGetText("viewSelect")
	Call session.SetEnvironmentVar("xdemo_viewSelect", fieldVal)
	
	If (fieldVal = "By Name") Then
		Call session.SetEnvironmentVar("xdemo_view", "mbContactsName")
		Call ws.ReloadWindow()	'Sets initial focus, so .currentField is set
	ElseIf (fieldVal = "By Surname") Then
		Call session.SetEnvironmentVar("xdemo_view", "mbContactsSurname")
		Call ws.ReloadWindow()	'Sets initial focus, so .currentField is set
	ElseIf (fieldVal = "By State") Then
		Call session.SetEnvironmentVar("xdemo_view", "mbContactsState")
		Call ws.ReloadWindow()	'Sets initial focus, so .currentField is set
		Set db = session.CurrentDatabase
		Call ws.SetTargetFrame("view")
		Call ws.ComposeDocument(db.server, db.filePath, "statesView")
		' Can't reload window here, or you can't leave!
	End If
	Exit Sub
	
showErr:
	MsgBox "Error " & CStr(Err) & " on line " & CStr(Erl) & ": " & Error(Err)
	Exit Sub
End Sub

'++LotusScript Development Environment:2:2:emailExport:5:8
%REM
	Sub emailExport
	Description: Comments for Sub
%END REM
Sub emailExport(stateCity As String)
	Dim session As New NotesSession
	Dim db As NotesDatabase
	Dim view As NotesView
	Dim dc As NotesDocumentCollection
	Dim doc As NotesDocument
	Dim ids As String, hostname As String, filepath As String
	Dim request As NotesHTTPRequest

	On Error GoTo showErr

	Set db = session.Currentdatabase
	Set view = db.Getview("mbContactsState")
	Set dc = view.Getalldocumentsbykey(stateCity, True)
	Set doc = dc.Getfirstdocument()
	While Not doc Is Nothing
		ids = ids & |'| & doc.Universalid & |',|
		Set doc = dc.Getnextdocument(doc)
	Wend
	ids = "[" & Left(ids, Len(ids) - 1) & "]"
	hostname = session.getEnvironmentString("xdemo_hostname", False)
	filepath = session.getEnvironmentString("xdemo_filepath", False)
	
	Set request = session.Createhttprequest()
	Call request.Setheaderfield("secretKey", "d1p243eafde5272")
	Call request.Setheaderfield("Content-Type", "application/json")
	Call request.Setheaderfield("sendTo", session.Username)
	Call request.Post(hostname & "/" & filepath & "/EmailContacts.xsp", |{'data':| & ids & |}|)
	If InStr(request.Responsecode, "200 OK") = 0 Then
		MsgBox "Sorry, request failed", 16, "Error"
	Else
		MsgBox "Please check your email", 64, "Success"
	End If
	
	Exit Sub
	
showErr:
	MsgBox "Error " & CStr(Err) & " on line " & CStr(Erl) & ": " & Error(Err)
	Exit Sub
End Sub

'++LotusScript Development Environment:2:2:exitCitiesField:5:8
%REM
	Sub exitCitiesField
	Description: Comments for Sub
%END REM
Sub exitCitiesField
	Dim ws As New NotesUIWorkspace
	Dim session As New NotesSession
	Dim uidoc As NotesUIDocument
	Set uidoc = ws.CurrentDocument
	
	On Error GoTo showErr
	
	Dim db As NotesDatabase
	Call session.SetEnvironmentVar("xdemo_city", uidoc.FieldGetText("cities"))
	Call ws.Reloadwindow()	'Sets initial focus, so .currentField is set
	
	Set db = session.CurrentDatabase
	Call ws.SetTargetFrame("view")
	Call ws.ComposeDocument(db.server, db.filePath, "statesView")
Exit Sub
	
showErr:
	MsgBox "Error " & CStr(Err) & " on line " & CStr(Erl) & ": " & Error(Err)
	Exit sub
End Sub

'++LotusScript Development Environment:2:2:exitStatesField:5:8
%REM
	Sub exitStatesField
	Description: Comments for Sub
%END REM
Sub exitStatesField
	Dim ws As New NotesUIWorkspace
	Dim session As New NotesSession
	Dim uidoc As NotesUIDocument
	Dim doc As NotesDocument
	Dim currValue As String
	
	On Error GoTo showErr
	
	Set uidoc = ws.CurrentDocument
	Dim db As NotesDatabase
	
	Call session.SetEnvironmentVar("xdemo_state", uidoc.Fieldgettext("states"))
	Call session.SetEnvironmentVar("xdemo_city", "All")
	Call ws.Reloadwindow()	'Sets initial focus, so .currentField is set
	
	Set db = session.CurrentDatabase
	Call ws.SetTargetFrame("view")
	Call ws.ComposeDocument(db.server, db.filePath, "statesView")
Exit Sub
	
showErr:
	MsgBox "Error " & CStr(Err) & " on line " & CStr(Erl) & ": " & Error(Err)
	Exit Sub
End Sub